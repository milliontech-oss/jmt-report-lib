apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'project-report' // use `gradlew htmlDependencyReport` to view the graphs
apply plugin: 'maven-publish'

group = 'com.milliontech'
version = '4.2.6'
sourceCompatibility = 1.8

println "Gradle Version: ${project.getGradle().getGradleVersion().toString()}"

repositories {
    //mavenCentral()
    maven {
        url 'https://int.milliontech.com/mtnexus/repository/maven-public/'
    }
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

publishing {
    repositories {
        mavenLocal()
        maven {
            name 'MT'
            def releasesRepoUrl = 'https://int.milliontech.com/mtnexus/repository/MT/'
            def snapshotsRepoUrl = 'https://int.milliontech.com/mtnexus/repository/maven-snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jmt-report-lib'
            artifact tasks.sourceJar
            from components.java
        }
    }
}

def apachePOIVersion = '5.0.0'
def slf4jVersion = '1.7.30'

java {
    registerFeature("csvSupport") {
        usingSourceSet(sourceSets.main)
    }
}

dependencies {
    modules {
        module("commons-logging:commons-logging") {
            replacedBy("org.slf4j:jcl-over-slf4j")
        }
    }

    // slf4j
    api("org.slf4j:slf4j-api:${slf4jVersion}");

    //POI
    implementation("org.apache.poi:poi:${apachePOIVersion}")
    implementation("org.apache.poi:poi-ooxml:${apachePOIVersion}")
    implementation("org.apache.poi:poi-ooxml-lite:${apachePOIVersion}")
    implementation("org.apache.poi:poi-scratchpad:${apachePOIVersion}")

    api('com.itextpdf:itextpdf:5.5.13')
    api('com.itextpdf:itext-pdfa:5.5.13')
    api('com.itextpdf:itext-asian:5.2.0')

    csvSupportApi('net.sf.supercsv:super-csv:2.4.0')

    api('org.jodd:jodd-bean:5.0.6')
    api('org.apache.commons:commons-lang3:3.9')
    api('org.apache.commons:commons-collections4:4.4')
    api('commons-io:commons-io:2.6')

    // Use JUnit test framework
    testImplementation('junit:junit:4.12')
    testImplementation("org.slf4j:jcl-over-slf4j:${slf4jVersion}")
    testImplementation('ch.qos.logback:logback-classic:1.2.3')
}


def listrepos = {
    println "~~~~ listing repositories ~~~~"
    project.repositories.each { println "  Name: ${it.name} =>  url: ${it.url}" }
    println "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"
}
listrepos()

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs.addAll([
             "-Xlint:all",
             "-Xlint:-serial",
             "-Xlint:-unchecked",
             "-Xlint:-rawtypes",
             "-Xlint:-cast",
             "-parameters"
        ])
        doFirst {
            println "current JVM = ${System.properties['java.vendor']} (${System.properties['java.version']}) (${System.properties['java.home']})"
            println "javac compiler flags => $options.allCompilerArgs"
        }
    }
}

test {
    dependsOn 'cleanTest'

    useJUnit()
    minHeapSize = "256m"
    maxHeapSize = "2G"
    testLogging.showStandardStreams = true
}
